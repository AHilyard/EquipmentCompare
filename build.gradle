buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url = "https://repo.spongepowered.org/maven" }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

archivesBaseName = project.name + '-' + project.mcVersion

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

mixin {
	add sourceSets.main, "${project.name.toLowerCase()}.refmap.json"
}

minecraft {
	mappings channel: 'official', version: project.mcVersion
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			properties 'forge.logging.markers': ''
			properties 'forge.logging.console.level': 'debug'
			properties 'terminal.ansi': 'true'
			workingDirectory project.file('run').canonicalPath
			source sourceSets.main

			mods {
				equipmentcompare {
					source sourceSets.main
				}
			}
		}
	}
}

repositories {
	maven {
		name = "CurseMaven"
		url "https://www.cursemaven.com"
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:' + project.mcVersion + '-' + project.forgeVersion
	annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'

	implementation fg.deobf('curse.maven:iceberg-520110:3943132')
	compileClasspath fg.deobf('curse.maven:legendarytooltips-532127:3943137')
	compileClasspath fg.deobf('curse.maven:curios-309927:3343065')
	compileClasspath fg.deobf('curse.maven:baubles-366844:3339230')
}

jar {
	manifest {
		attributes(["Specification-Title":		project.name,
					"Specification-Vendor":		project.author,
					"Specification-Version":	project.version,
					"Implementation-Title":		project.name,
					"Implementation-Version":	project.version,
					"Implementation-Vendor" :	project.author,
					"Implementation-Timestamp":	new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
					"MixinConfigs":				"${project.name.toLowerCase()}.mixins.json"],)
	}
}
