plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.name + '-' + project.mcVersion + '-fabric'

sourceSets.main.java.srcDirs += 'java'
sourceSets.main.resources.srcDirs += 'resources'

repositories {
	maven {
		name = "CurseMaven"
		url = "https://www.cursemaven.com"
		content { includeGroup "curse.maven" }
	}
	maven {
		name = "Fuzs Mod Resources"
		url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
	}
	maven {
		name = "Ladysnake Libs"
		url = "https://maven.ladysnake.org/releases"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${mcVersion}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${loaderVersion}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricVersion}"

	modImplementation files("libs/Iceberg-1.20.1-fabric-1.1.10.jar")
	modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forgeConfigApiVersion}"
	modApi files("libs/LegendaryTooltips-1.20.1-fabric-1.4.3.jar")

	// These are all needed for Trinkets.
	modApi include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${ccaVersion}")
	modApi include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${ccaVersion}")
	modImplementation "curse.maven:trinkets-341284:4762647"

	modImplementation "curse.maven:roughlyenoughitems-310111:4878207"
}

loom {
	accessWidenerPath = file("src/main/resources/${project.name.toLowerCase()}.accesswidener")
}

processResources {
	inputs.property 'version', project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

jar {
	manifest {
		attributes(["Specification-Title":		project.name,
					"Specification-Vendor":		project.author,
					"Specification-Version":	project.version,
					"Implementation-Title":		project.name,
					"Implementation-Version":	project.version,
					"Implementation-Vendor" :	project.author,
					"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
	}
}
