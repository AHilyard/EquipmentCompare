buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url = "https://repo.spongepowered.org/maven" }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

archivesBaseName = project.name + '-' + project.mcVersion

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
	mappings channel: 'snapshot', version: '20180814-1.12'
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			properties 'forge.logging.markers': ''
			properties 'forge.logging.console.level': 'debug'
			properties 'terminal.ansi': 'true'
			workingDirectory project.file('run').canonicalPath
			source sourceSets.main

			mods {
				equipmentcompare {
					source sourceSets.main
				}
			}
		}
	}
}

repositories {
	maven {
		name = "CurseMaven"
		url = "https://www.cursemaven.com"
	}
	maven {
		name = "Mixin"
		url = 'https://repo.spongepowered.org/maven'
	}
}

configurations {
	embed
	implementation.extendsFrom(embed)
}

dependencies {
	minecraft 'net.minecraftforge:forge:' + project.mcVersion + '-' + project.forgeVersion

	compileClasspath fg.deobf('curse.maven:legendarytooltips-532127:3622425')
	
	embed('org.spongepowered:mixin:0.8') {
		transitive = false
		exclude module: 'guava'
		exclude module: 'commons-io'
		exclude module: 'gson'
	}

	annotationProcessor 'org.spongepowered:mixin:0.8.4:processor'

	compileClasspath fg.deobf('curse.maven:baubles-227083:2518667')
	compileClasspath fg.deobf('curse.maven:aether-255308:3280119')
}

mixin {
	add sourceSets.main, "${project.name.toLowerCase()}.refmap.json"
	config "equipmentcompare.mixins.json"
}

processResources {
	duplicatesStrategy 'include'
	inputs.property "version", project.version
	inputs.property "mcVersion", project.mcVersion

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		expand 'version': project.version, 'mcVersion': project.mcVersion
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

jar {
	from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
		exclude 'LICENSE.txt', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
	}

	manifest {
		attributes(["Specification-Title":		project.name,
					"Specification-Vendor":		project.author,
					"Specification-Version":	project.version,
					"Implementation-Title":		project.name,
					"Implementation-Version":	project.version,
					"Implementation-Vendor" :	project.author,
					"Implementation-Timestamp":	new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
					"MixinConfigs":				"${project.name.toLowerCase()}.mixins.json",
					"FMLAT":					"accesstransformer.cfg",
					"ForceLoadAsMod":			"true",
					"TweakClass":				"org.spongepowered.asm.launch.MixinTweaker",
					"TweakOrder":				"0"],)
	}
}