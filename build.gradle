buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url = "https://repo.spongepowered.org/maven" }
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
	}
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

archivesBaseName = name + '-' + mcVersion

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

mixin {
	add sourceSets.main, "equipmentcompare.refmap.json"
}

minecraft {
	mappings 'snapshot', project.properties.mappingsVersion
	// accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	runs {
		client {
			property 'forge.logging.console.level', 'debug'
			property 'terminal.ansi', 'true'
			workingDirectory project.file('run').canonicalPath
			source sourceSets.main

			arg "-mixin.debug=true"
			arg "-mixin.checks.interfaces=true"
			arg "-mixin.config=equipmentcompare.mixins.json"

			mods {
				equipmentcompare {
					source sourceSets.main
				}
			}
		}
	}
}

repositories {
	maven {
		name = "CurseMaven"
		url "https://www.cursemaven.com"
	}
	maven {
		// For fernflower
		name = 'Sponge'
		url = 'https://repo.spongepowered.org/maven'
	}
}

// Process resources on build
processResources {
	// This will ensure that this task is redone when the versions change.
	inputs.property 'version', project.version

	// Replace stuff in mods.toml, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'META-INF/mods.toml'

		// Replace version
		expand 'version':project.version
	}

	// Copy everything else except the mods.toml
	from(sourceSets.main.resources.srcDirs) {
		exclude 'META-INF/mods.toml'
	}
}

configurations {
	provided
}

sourceSets {
	main {
		resources {
			srcDir 'src/generated/resources'
		}
		compileClasspath += configurations.provided
	}
	fernflower {
		compileClasspath += main.output
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:' + project.properties.mcVersion + '-' + forgeVersion
	annotationProcessor 'org.spongepowered:mixin:0.8:processor'

	// Fernflower decompiler
	compile 'org.jetbrains.java.decompiler:fernflower:sponge-SNAPSHOT'

	provided fg.deobf('curse.maven:curios-309927:3343065')
}

jar {
	manifest {
		attributes(["Specification-Title": project.name,
					"Specification-Vendor": project.author,
					"Specification-Version": "24.0",
					"Implementation-Title": project.name,
					"Implementation-Version": project.version,
					"Implementation-Vendor" : project.author,
					"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
					"MixinConfigs": "equipmentcompare.mixins.json"],)
	}
}
